plugins {
    id 'java'
}

dependencies {
    implementation platform(libs.reactorBom)
    implementation(platform(libs.prometheusSimpleClientBom))

    implementation project(':micrometer-core')
    implementation project(':micrometer-observation')
    implementation(libs.colt)
    implementation(libs.logback12)
    implementation(libs.slf4jApi)

    ['atlas', 'prometheus', 'datadog', 'ganglia', 'elastic', 'graphite', 'jmx', 'influx', 'statsd', 'new-relic', 'cloudwatch2', 'signalfx', 'wavefront', 'dynatrace', 'azure-monitor', 'humio', 'appoptics', 'kairos', 'stackdriver', 'otlp'].each { sys ->
        implementation project(":micrometer-registry-$sys")
    }

    implementation libs.prometheusSimpleClientPushgateway
    implementation 'io.projectreactor.netty:reactor-netty-http'
    implementation libs.kafkaClients
    implementation(libs.kafkaJunit)
    implementation libs.grpcServices
    implementation libs.grpcInprocess

    implementation libs.openTelemetry.proto
    implementation 'io.opentelemetry:opentelemetry-sdk:1.42.1'
    implementation 'io.opentelemetry:opentelemetry-exporter-otlp:1.42.1'
}

configurations.all {
    // see: https://github.com/micrometer-metrics/micrometer/issues/3954
    exclude group: 'com.google.guava', module: 'listenablefuture'
    exclude group: 'io.grpc', module: 'grpc-netty-shaded'
}
